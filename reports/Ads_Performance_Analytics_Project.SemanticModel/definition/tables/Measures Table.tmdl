table 'Measures Table'
	lineageTag: b35907f9-8c5f-4b29-b631-7e82b98330b9

	measure 'Total Spend' =
			
			VAR Dias = SELECTEDVALUE('Tabla Periodos Dinamicos'[Dias], 9999)
			VAR UltimaFechaGlobal = CALCULATE(MAX(global_ads_performance[date]), ALL(global_ads_performance))
			VAR FechaInicio = IF(Dias == 9999, MIN('Dim_Calendar'[Date]), UltimaFechaGlobal - Dias)
			
			// Capturamos la fecha de la fila/punto actual del gráfico
			VAR FechaFila = SELECTEDVALUE('Dim_Calendar'[Date])
			
			RETURN
			IF(
			    ISFILTERED('Dim_Calendar'[Date]),
			    // LÓGICA PARA EL GRÁFICO (Eje X)
			    IF(
			        FechaFila > FechaInicio && FechaFila <= UltimaFechaGlobal,
			        SUM(global_ads_performance[ad_spend]),
			        BLANK() // Esto limpia el eje X para los días que no quieres ver
			    ),
			    // LÓGICA PARA LA TARJETA O TOTALES
			    CALCULATE(
			        SUM(global_ads_performance[ad_spend]),
			        KEEPFILTERS('Dim_Calendar'[Date] > FechaInicio && 'Dim_Calendar'[Date] <= UltimaFechaGlobal)
			    )
			)
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		lineageTag: b0aada5f-710c-40f8-95f1-3b5806c33b33

		annotation PBI_FormatHint = {"currencyCulture":"es-US"}

	measure 'Total Revenue' =
			
			SUM(
			    global_ads_performance[revenue]
			)
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		lineageTag: 3250d12c-7129-4dba-9f39-dde382445121

		annotation PBI_FormatHint = {"currencyCulture":"es-US"}

	measure 'Total Conversions' =
			
			SUM(
			    global_ads_performance[conversions]
			)
		formatString: #,0
		lineageTag: 76ac75cf-8609-4fd8-9410-2148b44c0315

	measure 'Global ROAS' =
			
			DIVIDE(
			    [Total Revenue],
			    [Total Spend],
			    0
			)
		formatString: 0.00
		lineageTag: 8f33bccd-a012-4e92-bc69-ffd06b72fbe8

	measure 'Total Clicks' =
			
			SUM(
			    global_ads_performance[clicks]
			)
		formatString: 0
		lineageTag: 64ae23cf-7158-4b7b-b47a-464234ddbf0a

	measure 'Total Impressions' =
			
			VAR Dias = SELECTEDVALUE('Tabla Periodos Dinamicos'[Dias], 9999)
			VAR UltimaFechaGlobal = CALCULATE(MAX(global_ads_performance[date]), ALL(global_ads_performance))
			VAR FechaInicio = IF(Dias == 9999, MIN('Dim_Calendar'[Date]), UltimaFechaGlobal - Dias)
			
			// Esta variable detecta si la fecha de la fila actual del gráfico está en el rango
			VAR FechaFila = SELECTEDVALUE('Dim_Calendar'[Date])
			
			RETURN
			IF(
			    ISFILTERED('Dim_Calendar'[Date]),
			    // LÓGICA PARA EL GRÁFICO (Eje X)
			    IF(
			        FechaFila > FechaInicio && FechaFila <= UltimaFechaGlobal,
			        SUM(global_ads_performance[impressions]),
			        BLANK() // Al devolver BLANK, Power BI quita el día del gráfico
			    ),
			    // LÓGICA PARA LA TARJETA (Total acumulado)
			    CALCULATE(
			        SUM(global_ads_performance[impressions]),
			        KEEPFILTERS('Dim_Calendar'[Date] > FechaInicio && 'Dim_Calendar'[Date] <= UltimaFechaGlobal)
			    )
			)
		formatString: 0
		lineageTag: 3f601538-0041-41e4-8cce-907ad0ffddde

	measure 'Global CTR' =
			
			DIVIDE(
			    [Total Clicks],
			    [Total Impressions],
			    0
			)
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		lineageTag: 7e9c0dd9-710f-4f8d-908b-b066829f0068

	measure 'Global CPA' =
			
			DIVIDE(
			    [Total Spend],
			    [Total Conversions],
			    0
			)
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		lineageTag: 359d2a52-d1d1-4ab0-8951-421772a04605

		annotation PBI_FormatHint = {"currencyCulture":"es-US"}

	measure 'Global CPC' =
			
			DIVIDE(
			    [Total Spend],
			    [Total Clicks],
			    0
			)
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		lineageTag: 60ce390f-1bd6-4fea-ba3f-cd757b646e36

		annotation PBI_FormatHint = {"currencyCulture":"es-US"}

	measure 'Total Profit' = [Total Revenue] - [Total Spend]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		lineageTag: 4b163f27-1d07-405b-a62e-54b5fd740ba2

		annotation PBI_FormatHint = {"currencyCulture":"es-US"}

	measure 'Global ROI' =
			
			DIVIDE(
			    [Total Profit],
			    [Total Spend],
			    0
			)
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		lineageTag: bf84e3d5-3e65-47a9-b252-21c3a27145c4

	measure AOV =
			
			DIVIDE(
			    [Total Revenue],
			    [Total Conversions],
			    0
			)
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		lineageTag: 798f9183-4986-4fe7-9f48-71e570e10f3e

		annotation PBI_FormatHint = {"currencyCulture":"es-US"}

	measure 'Global CPM' =
			
			DIVIDE(
			    [Total Spend],
			    [Total Impressions],
			    0
			) * 1000
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		lineageTag: cd0b1ff0-9d24-4bb6-abbe-10623bd67b54

		annotation PBI_FormatHint = {"currencyCulture":"es-US"}

	measure 'Conversion Rate (CVR)' =
			
			DIVIDE(
			    [Total Conversions],
			    [Total Clicks],
			    0
			)
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		lineageTag: 732fde3c-66a6-499b-b122-cd485ab33789

	measure 'Campaings Records' = COUNTROWS(global_ads_performance)
		formatString: #,0
		lineageTag: 49e3aab0-6bdc-4ead-98a6-aaf2c50d8188

	measure 'Titulo de Ultima Fecha' =
			
			"Bienvenido, última actualización: " & FORMAT(MAX(global_ads_performance[date]), "dd/MM/yyyy")
		lineageTag: c2972acb-97d4-402d-bce7-5a263c7c0086

	measure 'Filtro Periodo Dinamico' =
			
			VAR DiasSeleccionados = SELECTEDVALUE('Tabla Periodos Dinamicos'[Dias], 9999)
			VAR UltimaFecha = CALCULATE(MAX(global_ads_performance[date]), ALL(global_ads_performance))
			VAR FechaInicio = UltimaFecha - DiasSeleccionados
			
			VAR FechaActual = MAX('Dim_Calendar'[Date])
			
			RETURN
			IF(
			    FechaActual > FechaInicio && FechaActual <= UltimaFecha,
			    1,
			    0
			)
		formatString: 0
		lineageTag: 2f670433-b8a2-4ca6-92e9-b03b773f0c5e

	measure 'Spend PM' = ```
			
			CALCULATE(
			    [Total Spend], 
			    DATEADD('Dim_Calendar'[Date], 
			    -1, 
			    MONTH
			    )
			)
			```
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		lineageTag: 6070b843-81ad-4ce0-9bd9-a424bf39881c

		annotation PBI_FormatHint = {"currencyCulture":"es-US"}

	measure 'Spend Var %' = ```
			
			DIVIDE(
			    [Total Spend] - [Spend PM], 
			    [Spend PM], 
			    0
			    )
			```
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		lineageTag: 76103690-4747-4136-ae67-5bf6c52f4257

	measure 'Spend Previous Period' = ```
			
			VAR DaysSelected = SELECTEDVALUE('Tabla Periodos Dinamicos'[Dias], 30)
			// Usamos la misma lógica de fecha máxima global que en Total Spend
			VAR UltimaFechaGlobal = CALCULATE(MAX(global_ads_performance[date]), ALL(global_ads_performance))
			VAR CurrentPeriodStart = UltimaFechaGlobal - DaysSelected
			
			RETURN
			CALCULATE(
			    SUM(global_ads_performance[ad_spend]), // Suma directa para evitar el BLANK() del ISFILTERED
			    REMOVEFILTERS('Tabla Periodos Dinamicos'),
			    REMOVEFILTERS('Dim_Calendar'),
			    'Dim_Calendar'[Date] > (CurrentPeriodStart - DaysSelected) && 
			    'Dim_Calendar'[Date] <= CurrentPeriodStart
			)
			```
		lineageTag: 1c03dcb0-8543-4bd6-a257-ff585747ba62

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Spend Var % Dynamic' =
			
			DIVIDE([Total Spend] - [Spend Previous Period], [Spend Previous Period], 0)
		lineageTag: 31d7a35d-2a2d-430b-90dc-c0995a73b8bf

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Spend Delta Text Dynamic' = ```
			
			VAR Delta = [Total Spend] - [Spend Previous Period]
			VAR DeltaPct = [Spend Var % Dynamic]
			VAR PeriodoSeleccionado = SELECTEDVALUE('Tabla Periodos Dinamicos'[Periodo], "Periodo")
			
			// Creamos un sufijo dinámico basado en la selección del usuario
			VAR SufijoDinamico = 
			    SWITCH(
			        PeriodoSeleccionado,
			        "Últimos 7 Días", "7d ant.",
			        "Últimos 14 Días", "14d ant.",
			        "Últimos 28 Días", "28d ant.",
			        "Últimos 30 Días", "30d ant.",
			        "Últimos 90 Días", "90d ant.",
			        "Todo", "al total",
			        "per. ant." // Valor por defecto
			    )
			
			RETURN
			IF (
			    ISBLANK ( [Spend Previous Period] ) || [Spend Previous Period] = 0 || PeriodoSeleccionado = "Todo",
			    "Dataset completo",
			    FORMAT ( Delta, "+$#,##0;-$#,##0" ) & " (" &
			    FORMAT ( DeltaPct, "0.0%" ) &
			    IF ( Delta >= 0, " ↑", " ↓" ) & ") vs " & SufijoDinamico
			)
			```
		lineageTag: d277d536-e1ea-4358-9a4d-d04d315a4895

	measure 'Spend vs Impr Sparkline SVG' = ```
			
			
			// 1. Configuración de Colores
			
			VAR LineColor = "#0288D1"
			
			VAR BarColor = "#B3E5FC"
			
			VAR PointColor = "#01579B"
			
			
			// 2. Parámetros del Lienzo
			
			VAR Width = 150
			
			VAR Height = 50
			
			VAR Padding = 2
			
			
			// 3. Captura de Periodo (Sincronizado con el segmentador)
			
			VAR DiasSeleccionados = SELECTEDVALUE('Tabla Periodos Dinamicos'[Dias], 9999)
			
			VAR UltimaFecha = CALCULATE(MAX(global_ads_performance[date]), ALL(global_ads_performance))
			
			VAR FechaInicio = IF(DiasSeleccionados = 9999, MIN(global_ads_performance[date]), UltimaFecha - DiasSeleccionados)
			
			
			// 4. Crear la tabla de tendencia con VALORES DIARIOS
			
			VAR TrendTable =
			
			    FILTER(
			
			        SUMMARIZE(
			
			            ALL('Dim_Calendar'),
			
			            'Dim_Calendar'[Date],
			
			            "DailySpend", SUM(global_ads_performance[ad_spend]),
			
			            "DailyImpr", SUM(global_ads_performance[impressions])
			
			        ),
			
			        'Dim_Calendar'[Date] > FechaInicio && 'Dim_Calendar'[Date] <= UltimaFecha
			
			    )
			
			
			// 5. Máximos para escalado (basado en la tabla de tendencia)
			
			VAR MaxSpend = MAXX(TrendTable, [DailySpend])
			
			VAR MaxImpr = MAXX(TrendTable, [DailyImpr])
			
			VAR XMinDate = MINX(TrendTable, [Date])
			
			VAR XMaxDate = MAXX(TrendTable, [Date])
			
			VAR DateCount = COUNTROWS(TrendTable)
			
			
			// 6. Generar Coordenadas
			
			VAR SparklineTable =
			
			    ADDCOLUMNS(
			
			        TrendTable,
			
			        "X", INT( (Width - (Padding * 2)) * DIVIDE([Date] - XMinDate, XMaxDate - XMinDate) ) + Padding,
			
			        "Y_Line", INT( (Height - (Padding * 2)) * DIVIDE([DailySpend], MaxSpend) ),
			
			        "Y_Bar", INT( (Height - (Padding * 2)) * DIVIDE([DailyImpr], MaxImpr) )
			
			    )
			
			
			// 7. Renderizado de Barras (Impresiones)
			
			VAR BarWidth = DIVIDE(Width, DateCount) * 0.8
			
			VAR Bars =
			
			    CONCATENATEX(
			
			        SparklineTable,
			
			        "<rect x='" & [X] - (BarWidth/2) & "' y='" & Height - [Y_Bar] & "' width='" & BarWidth & "' height='" & [Y_Bar] & "' fill='" & BarColor & "' />"
			
			    )
			
			
			// 8. Renderizado de Línea (Gasto)
			
			VAR LinePoints = CONCATENATEX(SparklineTable, [X] & "," & Height - [Y_Line], " ", [Date])
			
			
			// 9. Punto Final
			
			VAR LastX = MAXX(FILTER(SparklineTable, [Date] = XMaxDate), [X])
			
			VAR LastY = Height - MAXX(FILTER(SparklineTable, [Date] = XMaxDate), [Y_Line])
			
			
			// 10. SVG Final
			
			VAR SVG =
			
			    "data:image/svg+xml;utf8," &
			
			    "<svg xmlns='http://www.w3.org/2000/svg' viewBox='0 0 " & Width & " " & Height & "'>" &
			
			        Bars &
			
			        "<polyline fill='none' stroke='" & LineColor & "' stroke-width='2' stroke-linejoin='round' stroke-linecap='round' points='" & LinePoints & "' />" &
			
			        "<circle cx='" & LastX & "' cy='" & LastY & "' r='2' fill='" & PointColor & "' />" &
			
			    "</svg>"
			
			
			RETURN SVG 
			```
		lineageTag: 68374cda-61a3-45c3-a495-8dcc7736ab37
		dataCategory: ImageUrl

	partition 'Measures Table' = m
		mode: import
		source =
				let
				    Origen = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i44FAA==", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [Columna1 = _t]),
				    #"Tipo cambiado" = Table.TransformColumnTypes(Origen,{{"Columna1", type text}}),
				    #"Columnas quitadas" = Table.RemoveColumns(#"Tipo cambiado",{"Columna1"})
				in
				    #"Columnas quitadas"

	annotation PBI_NavigationStepName = Navegación

	annotation PBI_ResultType = Table

